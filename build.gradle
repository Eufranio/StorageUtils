buildscript {
    repositories {
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
}

plugins {
    id 'maven'
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'java-library'

group 'io.github.eufranio'
version '2.4'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    shadow 'org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT'
    shadow 'org.spongepowered:spongeapi:7.2.0-SNAPSHOT'

    api 'org.spongepowered:configurate-core:3.6.1'
    api 'org.spongepowered:configurate-hocon:3.6.1'
    api 'com.j256.ormlite:ormlite-jdbc:5.1'
    api 'com.j256.ormlite:ormlite-core:5.1'
}

shadowJar {
    archiveClassifier = System.getenv('JITPACK') != null ? 'all' : null
    from sourceSets.main.output

    relocate 'ninja.leaping.configurate', 'storageutils.ninja.leaping.configurate'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

build.dependsOn shadowJar
install.dependsOn shadowJar
